set(ROOT_DIR ${CMAKE_CURRENT_SOURCE_DIR}/..)

set(INCLUDE_DIR ${ROOT_DIR}/tests)
set(HEADER_FILES
	${INCLUDE_DIR}/utility.hpp
)
source_group("include" FILES ${HEADER_FILES})

set(SOURCE_DIR ${ROOT_DIR}/tests)
set(SOURCE_FILES
	${SOURCE_DIR}/common_tests.cpp
	${SOURCE_DIR}/fo4_tests.cpp
	${SOURCE_DIR}/main.cpp
	${SOURCE_DIR}/tes3_tests.cpp
	${SOURCE_DIR}/tes4_tests.cpp
)
source_group("src" FILES ${SOURCE_FILES})

set(NATVIS_DIR ${ROOT_DIR}/extern/CPPDebuggerVisualizers/VS2019/Visualizers)
set(NATVIS_FILES
	${NATVIS_DIR}/boost.natvis
	${NATVIS_DIR}/boost_Containers.natvis
	${NATVIS_DIR}/boost_Regex.natvis
)
source_group("visualizers" FILES ${NATVIS_FILES})

add_executable(
	tests
	${HEADER_FILES}
	${SOURCE_FILES}
	${NATVIS_FILES}
)
catch_discover_tests(tests)

target_compile_definitions(
	tests
	PRIVATE
		_CRT_SECURE_NO_WARNINGS
)

find_package(Boost
	MODULE
	REQUIRED
	COMPONENTS
		iostreams
		nowide
		regex
)

target_link_libraries(
	tests
	PRIVATE
		Boost::iostreams
		Boost::nowide
		Boost::regex
		bsa::bsa
		bsa::common
		Catch2::Catch2
)

function(download_test)
	set(_PREFIX download_file)
	set(ONE_VALUE_ARGS
		NAME
		SHA512
		TAG
	)

	cmake_parse_arguments(${_PREFIX} "" "${ONE_VALUE_ARGS}" "" ${ARGN})

	if(DEFINED "${_PREFIX}_KEYWORDS_MISSING_VALUES")
		foreach(ARG "${_PREFIX}_KEYWORDS_MISSING_VALUES")
			if(NOT DEFINED "${_PREFIX}_${ARG}")
				message(FATAL_ERROR "missing required argument: ${ARG}")
			endif()
		endforeach()
	endif()

	if(DEFINED "${_PREFIX}_UNPARSED_ARGUMENTS")
		foreach(ARG "${_PREFIX}_UNPARSED_ARGUMENTS")
			if(NOT DEFINED "${_PREFIX}_${ARG}")
				message(FATAL_ERROR "unused argument: ${ARG}")
			endif()
		endforeach()
	endif()

	set(NAME "${${_PREFIX}_NAME}")
	set(OUT_FILE "${CMAKE_CURRENT_BINARY_DIR}/${NAME}.7z")

	file(DOWNLOAD
		"https://github.com/Ryan-rsm-McKenzie/bsa/releases/download/${${_PREFIX}_TAG}/${NAME}.7z"
		${OUT_FILE}
		EXPECTED_HASH SHA512=${${_PREFIX}_SHA512}
	)

	file(ARCHIVE_EXTRACT
		INPUT ${OUT_FILE}
		DESTINATION "${CMAKE_CURRENT_BINARY_DIR}/${NAME}"
	)
endfunction()

download_test(
	NAME "tes3_invalid_test"
	TAG "0.0.3"
	SHA512 "68C7E8B8A09692BB9908932ADFCAB55A2C5426F1C2032973D4894D7B56980681FFAFA1B073524337C5B7883C196305E63E38329F00D639F61877DC87C678C801"
)

download_test(
	NAME "tes3_read_test"
	TAG "0.0.4"
	SHA512 "B79EBDE832FF70C897209A72AE4C1423FB39915DD0AA4040D23B2E2586F264E9A976B51BE9CE13746E7D40646572942B10F6CF3F0784027F7843F80A1D13D8E4"
)

download_test(
	NAME "tes3_write_test"
	TAG "0.0.3"
	SHA512 "9C495FF3348DF128B212FC2EDA4B1DD81250F379C07B256B2A4ACAC15815DB4A850AB969C1C8D382C3BD2154F5F2642EDE9E6E4CD56CA8088C511D12AE39E132"
)

download_test(
	NAME "tes4_compression_mismatch_test"
	TAG "0.0.3"
	SHA512 "A8FAAE5C2244867E3A29A7E035D24F208FD8E08D7AAA727678340C0D6D23C75265DDC0A74D6F8AF26AA7099ADEB67E3944F5607F707204C19184277A276C166B"
)

download_test(
	NAME "tes4_compression_test"
	TAG "0.0.3"
	SHA512 "453816F36D499A6F2D97FEE4FEFB9784E2B021EA9F4443942E021877F3FA483643BFC8C7FA7B610A0B3207EED2C81B2515E63EE4CCC43AEC911C5D9AB585227C"
)

download_test(
	NAME "tes4_flags_test"
	TAG "0.0.3"
	SHA512 "5323B6846AB8481DDFE2C6CC8E7CAB52DCCD087B9375F7D799C5DA7755300771693169F9D9D68E2881AFC5A56BAB72D20B15F34F959BA6C50BF3A673534DEFE1"
)

download_test(
	NAME "tes4_invalid_test"
	TAG "0.0.3"
	SHA512 "3F835E3180D10226AB2307C9AABC680DB246BA452BA9F0602FF1A0B16A3D9A1C93251B926ADA91974EA0970DA1B1266D58B073B6A563A06B1187E13DDECB8470"
)

download_test(
	NAME "tes4_xbox_read_test"
	TAG "0.0.4"
	SHA512 "DC4CAE7A06D02C1299D343AE53A098D6376F764A7152503E3C6685CF92EA97DD8C7873224FE39B0D1F6C3595B8A3BEA3397A7AA9F5748B2C40D89F0EEF8360AA"
)

download_test(
	NAME "tes4_xbox_write_test"
	TAG "0.0.4"
	SHA512 "D4CA909DB8669B1E540BA0C83FFD599A9398F80D927B652141667836B3A4010581BCC2C6D5317CF871339F6B77EFBAED2C235BEA6FA38CFF333A5174127F594B"
)
